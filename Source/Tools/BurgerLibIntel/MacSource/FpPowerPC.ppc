;
; Assembly for the Floating Point math manager
; Power PC version
; Optimized and written by Bill Heineman
; Copyright Bill Heineman
;

	IMPORT FSineTable

;
; int FloatToInt(float Input);
;

	EXPORT	FloatToInt[DS],.FloatToInt[PR]

	TOC
	tc	FloatToInt[TC],FloatToInt[DS]

	CSECT	FloatToInt[DS]
		dc.l	.FloatToInt[PR]
		dc.l	TOC[tc0]

	CSECT	.FloatToInt[PR]

		fctiw		fp0,fp1			;Convert to an integer (Round to nearest)
		stfd		fp0,-16(SP)		;Store in memory
		lwz			r3,-12(SP)		;Get the integer
		blr							;Exit with answer in r3
		
;
; float FloatSine(float Input);
;

	EXPORT	FloatSine[DS],.FloatSine[PR]

	TOC
		tc	FloatSine[TC],FloatSine[DS]
		tc	FSineTable[TC],FSineTable

	CSECT	FloatSine[DS]
		dc.l	.FloatSine[PR]
		dc.l	TOC[tc0]

	CSECT	.FloatSine[PR]

		fctiw	fp0,fp1				;Convert to an integer (Round to nearest) 
		lwz		r4,FSineTable[TC](RTOC)	;Get pointer to table
		stfd	fp0,-16(SP)			;Store the integer
		lwz		r3,-12(SP)			;Get the value
		clrlslwi r0,r3,21,2			;Mask with 0x7FF and shift up 2 bits
		lfsx	fp1,r4,r0			;Get the floating point sine
		blr							;Exit with answer in fp1

;
; float FloatCosine(float Input);
;

	EXPORT	FloatCosine[DS],.FloatCosine[PR]

	TOC
		tc	FloatCosine[TC],FloatCosine[DS]
		tc	FSineTable2048[TC],FSineTable+2048

	CSECT	FloatCosine[DS]
		dc.l	.FloatCosine[PR]
		dc.l	TOC[tc0]

	CSECT	.FloatCosine[PR]

		fctiw	fp0,fp1				;Convert to an integer (Round to nearest) 
		lwz		r4,FSineTable2048[TC](RTOC)	;Get pointer to table
		stfd	fp0,-16(SP)			;Store the integer
		lwz		r3,-12(SP)			;Get the value
		clrlslwi r0,r3,21,2			;Mask with 0x7FF and shift up 2 bits
		lfsx	fp1,r4,r0			;Get the floating point sine
		blr							;Exit with answer in fp1

		END

